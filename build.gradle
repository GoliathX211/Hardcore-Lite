plugins {
    id 'java'
}

// Change the values below this to your mods values
// To change the project name, edit the settings.gradle file

project.ext.modID = "goliath.hardcorelite" // The unique id of your mod. Must be all lowercase and cannot use special characters.
project.ext.modName = "Hardcore Lite" // The display name of your mod.
project.ext.modVersion = "1.1.1" // Your current builds version. Must follow the xx.xx... format.
project.ext.gameVersion = "0.25.1" // The target game version.
project.ext.modDescription = "Adds a new death penalty." // Short description of what your mod is.
project.ext.author = "butterflysky; GoliathX211; Deadly990" // Your name

/**
 * When setting clientside to true, it means servers do not need this mod for clients to connect and vice versa.
 * IMPORTANT: If you set this to true, make sure that your mod does not add any content or do anything
 * that could cause clients and servers to desync. This includes registering any items, objects, tiles, packets etc.
 */
project.ext.clientside = false

/**
 * The other mod dependencies of this mod
 * Dependencies define the default load order of mods
 * Uncomment and configure these if your mod has dependencies
 */
//project.ext.modDependencies = ["other.modid1", "other.modid2"]
//project.ext.modOptionalDependencies = ["optional.modid1", "optional.modid2"]

// The path to the games install directory
def gameDirectory = System.getProperty("game.path", System.getenv("HOME") + "/.local/share/Steam/steamapps/common/Necesse")

// =================================================
// ========== DO NOT EDIT BELOW THIS LINE ==========
// =================================================

// Name of the jar
def jarName = "${project.ext.modName.replace(" ", "")}-${project.ext.gameVersion}-${project.ext.modVersion}"

group project.ext.modID
version project.ext.modVersion

setSourceCompatibility(JavaVersion.VERSION_1_8)
setTargetCompatibility(JavaVersion.VERSION_1_8)

repositories {
    mavenCentral()
}

configurations {
    libDepends
}

def jarBuildLocation = "build/jar"
def modBuildLocation = "build/mod"

sourceSets {
    main {
        java {
            srcDir "src/main/java"
            destinationDirectory.set(file(modBuildLocation))
        }
        resources {
            srcDir "src/main/resources"
            output.resourcesDir = file(modBuildLocation + "/resources")
        }
        compileClasspath += configurations.libDepends
    }
}



compileJava.options.encoding = "UTF-8"
processResources.duplicatesStrategy = DuplicatesStrategy.EXCLUDE

if (!file(gameDirectory + "/Necesse.jar").exists()) {
    throw new Exception("Could not find game install directory. Make sure it is correct in build.gradle file.")
}

dependencies {
    implementation files(gameDirectory + "/Necesse.jar")
    implementation fileTree(gameDirectory + "/lib/")
    implementation fileTree("./mods/") // Add all mods located in local mods folder

    // Add any third party library dependencies here. Remember to use libDepends, so that they will be added to your jar on build
    // These are some constructorpatches:
    //    libDepends group: 'com.google.guava', name: 'guava', version: '31.1-jre'
    //    libDepends files("path/to/library/jar.jar")
}

tasks.register('createAppID') {
    group "necesse"
    description "Creates steam_appid.txt file"

    doLast {
        file("steam_appid.txt").text = "1169040"
    }
}

tasks.register('createModInfoFile', JavaExec) {
    group "necesse"
    description "Creates the mod info file"

    classpath = files(gameDirectory + "/Necesse.jar")

    main = "CreateModInfoFile"
    args "-file", "${modBuildLocation}/mod.info",
            "-id", "${project.ext.modID}",
            "-name", "${project.ext.modName}",
            "-version", "${project.ext.modVersion}",
            "-gameVersion", "${project.ext.gameVersion}",
            "-description", "${project.ext.modDescription}",
            "-author", "${project.ext.author}",
            "-clientside", "${project.ext.clientside}",
            "-dependencies", project.ext.has("modDependencies") ? "[" + project.ext.modDependencies.join(", ") + "]" : "",
            "-optionalDependencies", project.ext.has("modOptionalDependencies") ? "[" + project.ext.modOptionalDependencies.join(", ") + "]" : ""
}
// Makes compiling also create mod info file
classes.dependsOn("createModInfoFile")

tasks.register('runClient', JavaExec) {
    group "necesse"
    description "Run client with current mod"
    dependsOn "buildModJar", "createAppID"

    classpath = files(gameDirectory + "/Necesse.jar")

    main = "StartClient"
    jvmArgs "-Xms512m", "-Xmx4G", "-XX:+UnlockExperimentalVMOptions", "-XX:+UseG1GC", "-XX:G1NewSizePercent=20", "-XX:G1ReservePercent=20", "-XX:MaxGCPauseMillis=50", "-XX:G1HeapRegionSize=32M"
    args "-dev", "-mod \"${jarBuildLocation}\""
}

tasks.register('runDevClient', JavaExec) {
    group "necesse"
    description "Run client with current mod"
    dependsOn "buildModJar", "createAppID"

    classpath = files(gameDirectory + "/Necesse.jar")

    main = "StartClient"
    jvmArgs "-Xms512m", "-Xmx4G", "-XX:+UnlockExperimentalVMOptions", "-XX:+UseG1GC", "-XX:G1NewSizePercent=20", "-XX:G1ReservePercent=20", "-XX:MaxGCPauseMillis=50", "-XX:G1HeapRegionSize=32M"
    args "-dev 1", "-mod \"${jarBuildLocation}\""
}

tasks.register('runServer', JavaExec) {
    group "necesse"
    description "Run server with current mod"
    dependsOn "buildModJar"

    classpath = files(gameDirectory + "/Server.jar")

    main = "StartServer"
    jvmArgs "-Xms512m", "-Xmx4G", "-XX:+UnlockExperimentalVMOptions", "-XX:+UseG1GC", "-XX:G1NewSizePercent=20", "-XX:G1ReservePercent=20", "-XX:MaxGCPauseMillis=50", "-XX:G1HeapRegionSize=32M"
    args "-dev -mod \"${jarBuildLocation}\""
}

tasks.register('buildModJar', Jar) {
    group "necesse"
    description "Generates the mod jar into the build folder"
    dependsOn "classes"

    doFirst {
        fileTree(dir: jarBuildLocation).exclude("${jarName}.jar").visit { FileVisitDetails details ->
            delete details.file
        }
    }

    // Add mod output
    from sourceSets.main.java.destinationDirectory
    // Add the dependencies
    from configurations.libDepends.collect { it.isDirectory() ? it : zipTree(it) }

    archiveFileName.set("${jarName}.jar")
    destinationDirectory.set(file(jarBuildLocation))
}
